<?xml version="1.0"?>
<!DOCTYPE xsl:stylesheet [
  <!ENTITY % CommonConfig SYSTEM "../config/CommonConfig.dtd">
  %CommonConfig;
   ]>

<!--
  Copyright (C) 2003 by Institute for Systems Biology,
  Seattle, Washington, USA.  All rights reserved.
  
  This source code is distributed under the GNU Lesser 
  General Public License, the text of which is available at:
    http://www.gnu.org/copyleft/lesser.html
 -->

<xsl:stylesheet version="1.0" 
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
                xmlns:isb="http://www.systemsbiology.org/xmlns"
                xmlns:html="http://www.w3.org/TR/xhtml1"
                xmlns="http://www.w3.org/TR/xhtml1"
                >              
  <xsl:output method="html" indent="yes" encoding="ISO-8859-1" />

  <xsl:template match="html:*|html:*/@*">
    <xsl:copy>
      <xsl:apply-templates select="@* | node()" />
    </xsl:copy>
  </xsl:template>

  <xsl:template match="/">
    <html>
      <xsl:apply-templates/>
    </html>  
  </xsl:template>

  <xsl:template match="/isb:document">
    <head>
      <title><xsl:value-of select="isb:doctitle"/></title>
    </head>
    <body>
    <xsl:if test="/isb:document/isb:docpreamble != ''">
      <xsl:value-of select="/isb:document/isb:docpreamble" />

    </xsl:if>
    <table cellpadding="30">
      <tr>
        <td>
      <xsl:if test="/isb:document/isb:doctitle != ''">
        <h1><xsl:value-of select="isb:doctitle"/></h1>
      </xsl:if>

      <!-- if an author is specified, use the author information: -->

      <xsl:if test="/isb:document/isb:docowner != ''">
        <xsl:if test="/isb:document/isb:docownerurl != ''">
          <xsl:element name="a"> 
            <xsl:attribute name="href"><xsl:value-of select="/isb:document/isb:docownerurl" /></xsl:attribute>
            <span style="color: black"><h3><xsl:value-of select="isb:docowner"/></h3></span>
          </xsl:element>
        </xsl:if>
        <xsl:if test="not( /isb:document/isb:docownerurl != '')">
          <h3><xsl:value-of select="isb:docowner"/></h3>
        </xsl:if>
      </xsl:if>

      <!-- if an owner is not specified, generate boilerplate owner information with hyperlink: -->

      <xsl:if test="not( /isb:document/isb:docowner != '' )">
        <a href="&mainGroupURL;"><span style="color: black"><h3>&mainGroup;, &mainAffiliation;</h3></span></a>
      </xsl:if>

      <!-- if author information specified, display it: -->

      <xsl:if test="/isb:document/isb:docauthor != ''">
        <xsl:if test="/isb:document/isb:docauthorurl != ''">
          <xsl:element name="a">
            <xsl:attribute name="href"><xsl:value-of select="isb:docauthorurl" /></xsl:attribute>
            <span style="color: black">
            <h4>
              <xsl:value-of select="isb:docauthor" />
            </h4>
            </span>
          </xsl:element>
        </xsl:if>
        <xsl:if test="not( isb:docauthorurl != '' )">
          <h4>
            <xsl:value-of select="isb:docauthor" />
          </h4>
        </xsl:if>
      </xsl:if>
 
      <!-- if version information specified, strip out RCS keyword stuff ($Date ... $) and display: -->

      <xsl:if test="/isb:document/isb:docversion != ''">
        <h4>
          <xsl:value-of select="isb:docversion"/>
        </h4>
      </xsl:if>



        </td>
        <td>
          <a href="&mainPublicURL;"><img src="&mainLogoURL;" alt="ISB" border="0" /></a>
        </td>
        <td>
          <a href="&mainGroupURL;"><img src="&groupLogoURL;" alt="Bolouri Group" border="0" /></a>
        </td>
      </tr>
      </table>
      <hr noshade="true" />
      <xsl:apply-templates/>

      <hr noshade="true" />

      <xsl:if test="/isb:document/isb:docdate != ''">
        <p>
        <b>Last updated:
          <xsl:if test="contains(isb:docdate, '$Date:')">
            <xsl:value-of select="translate(concat(
                                      substring-before(isb:docdate, '$Date:'), 
                                      substring-after(isb:docdate, '$Date:')),'$','')"/>
          </xsl:if>
          <xsl:if test="not( contains( isb:docdate, '$Date:') )">
            <xsl:value-of select="isb:docdate"/>
          </xsl:if>
        </b>
        </p>
      </xsl:if>
 
      <p>
      <b>Please e-mail comments or corrections regarding this document to: 
        <code>
          <xsl:if test="/isb:document/isb:docowneremail != ''">
            <xsl:value-of select="isb:docowneremail" />
          </xsl:if>
          <xsl:if test="not( /isb:document/isb:docowneremail != '')">
            &mainGroupEmailSafe;
          </xsl:if>
        </code>
      </b>
      </p>  

    </body>
  </xsl:template>
 
  <xsl:template match="isb:doctitle"></xsl:template>
  <xsl:template match="isb:docowner"></xsl:template>
  <xsl:template match="isb:docownerurl"></xsl:template>
  <xsl:template match="isb:docowneremail"></xsl:template>
  <xsl:template match="isb:docversion"></xsl:template>
  <xsl:template match="isb:docdate"></xsl:template>
  <xsl:template match="isb:docpreamble"></xsl:template>
  <xsl:template match="isb:docauthor"></xsl:template>
  <xsl:template match="isb:docauthorurl"></xsl:template>

  <xsl:template match="isb:doccontents">
      <h3>Contents</h3>
      <ol>
      <xsl:for-each select="/isb:document/isb:docsection">
        <li>
        <xsl:if test="@label != ''">
          <xsl:element name="a">
            <xsl:attribute name="href">#<xsl:value-of select="@label" /></xsl:attribute>
            <b><xsl:value-of select="@name" /></b>
          </xsl:element>
        </xsl:if>
        <xsl:if test="not (@label != '')">
            <b><xsl:value-of select="@name" /></b>
        </xsl:if>
        <xsl:if test="isb:docsubsection != ''">
          <ul>
          <xsl:for-each select="isb:docsubsection">
            <li>
              <xsl:if test="@label != ''">
                <xsl:element name="a">
                  <xsl:attribute name="href">#<xsl:value-of select="@label" /></xsl:attribute>
                  <b><xsl:value-of select="@name" /></b>
                </xsl:element>
              </xsl:if>
              <xsl:if test="not (@label != '')">
                  <b><xsl:value-of select="@name" /></b>
              </xsl:if>
            </li>
          </xsl:for-each>
          </ul>
        </xsl:if>
        </li>
      </xsl:for-each>
      </ol>
      <hr noshade="true" />
  </xsl:template>

  <xsl:template match="//isb:docsubsection">
    <xsl:if test="@label != ''">
      <xsl:element name="a">
        <xsl:attribute name="name"><xsl:value-of select="@label" /></xsl:attribute>
      </xsl:element>
    </xsl:if>
    <p><b><xsl:value-of select="@name" /></b></p>
    <xsl:apply-templates/>
  </xsl:template>

  <xsl:template match="//isb:docsection">
    <xsl:if test="@label != ''">
      <xsl:element name="a">
        <xsl:attribute name="name"><xsl:value-of select="@label" /></xsl:attribute>
      </xsl:element>
    </xsl:if>
    <h3><xsl:value-of select="@name" /></h3>
    <xsl:apply-templates/>
  </xsl:template>
  
</xsl:stylesheet>
